---
layout: user-guide
guide: 1
total_guides: 4
---
# 2 Diving In

## 2.1 Numbers and Operators

### 2.1.1 Integers and Floats

Let's start with something simple :-) To follow along, fire up your LFE REPL.
Numbers are simple in LFE, just like Erlang:
{% highlight cl %}
> 1
1
> 2
2
> 3
3
>
{% endhighlight %}

Of course, it might be more interesting to look at something like different
bases:
{% highlight cl %}
> #b101010
42
> #o52
42
> #x2a
42
> #36r16
42
>
{% endhighlight %}

With some help from calling an Erlang function, we can work this in reverse,
too:
{% highlight cl %}
> (: erlang integer_to_list 123 2)
"1111011"
{% endhighlight %}
Note that the first argument is the number you want to convert and the second
is the base you want to use (see
<a href="http://erldocs.com/R14B/erts/erlang.html?i=1&search=integer#integer_to_list/2">here</a>
for more details).

### 2.1.2 Arithmatic Operators

But numbers by themselves aren't going to do us much good if we can't operate
on them. The usual apply:
{% highlight cl %}
> (+ 1 2 3 4 5 6)
21
> (- 6 21)
15
> (/ 36 7)
5.142857142857143
> (+ #b101010 #o52 #x2a #36r16)
168
> (* 42 4)
168
> (: erlang integer_to_list (+ #b1001 #b100 #b10) 2)
"1111"
> (div 11 2)
5
> (rem 11 2)
1
> `(,(div 11 2) ,(rem 11 2))
(5 1)
>
{% endhighlight %}

### 2.1.3 Logical Operators

The usual suspects are used as follows:
{% highlight cl %}
> (< 1 2)
true
> (> 1 2)
false
> (>= 2 2)
true
> (=< 3 2)
false
> (>= 3 2)
true
> (== 1 1)
true
> (== 1 1.0)
true
> (/= 1 1)
false
> (/= 2 1)
true
>
{% endhighlight %}
Note the rather awkward different between "less than" and "greater than": it's
easy to forget that the angle brackets go at different ends for each.

Then there are the operators which also check against type for exact
(non-)equality: {% highlight cl %}
> (=:= 1 1.0)
false
> (=:= 1.0 1.0)
true
> (=/= 1.0 1.0)
false
> (=/= 1 1.0)
true
>
{% endhighlight %}

### 2.1.4 Boolean Operators

How about some logic?
{% highlight cl %}
> (and 'true 'false)
false
> (and 'true 'true)
true
> (or 'true 'true)
true
> (or 'true 'false)
true
> (or 'false 'false)
false
> (not 'false)
true
> (not 'true)
false
> (xor 'true 'true)
false
> (xor 'false 'false)
false
> (xor 'true 'false)
true
>
{% endhighlight %}

There are also two boolean operators that you can use if you want to make a
decision based on the truth value of the first term without having to compute
the second term, it not needed:
{% highlight cl %}
> (andalso 'false 1)
false
> (andalso 'true 1)
1
> (orelse 'true 1)
true
> (orelse 'false 1)
1
>
{% endhighlight %}
In the case of ```andalso``` if the first argument is ```false``` the second
one will not be evaluated; ```false``` will be returned. In the case of
```orelse``` if the first argument is ```true``` then ```true``` will be
returned without evaluating the second argument.

Contrast this to regular ```or``` and ```and```:
{% highlight cl %}
> (and 'false 1)
exception error: badarg
  in (: erlang and false 1)

> (and 'true 1)
exception error: badarg
  in (: erlang and true 1)

> (or 'false 1)
exception error: badarg
  in (: erlang or false 1)

> (or 'true 1)
exception error: badarg
  in (: erlang or true 1)

>
{% endhighlight %}

### 2.1.5 Bitwise Operators

As one would expect, Erlang has the usual bitwise operators as well. Binary
representation is used below for clarity of demonstration:
{% highlight cl %}
> (set dec2bin (lambda (x) (: erlang integer_to_list x 2)))
#Fun<lfe_eval.10.53503600>
>
{% endhighlight %}

With that defined so that we can use it, let's take a look at some of these
operators:
{% highlight cl %}
> (funcall dec2bin (band #b10001 #b1001))
"1"
> (funcall dec2bin (bor #b10001 #b1001))
"11001"
> (funcall dec2bin (bxor #b10001 #b1001))
"11000"
> (funcall dec2bin (bnot #b10001))
"-10010"
> (funcall dec2bin (bnot (bnot #b10001)))
"10001"
> (funcall dec2bin (bsl #b10001 1))
"100010"
> (funcall dec2bin (bsr #b10001 1))
"1000"
>
{% endhighlight %}
